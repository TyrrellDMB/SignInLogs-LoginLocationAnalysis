// This is an updated query that retrieves Azure sign in location data and compares the users last signin location. 
// Providing the analyst with a data set that shows login start time, user details, location, IP address & difference in location by miles. 
// You can use this to easily distinguish if a user is logging in from a new location/IP.

SigninLogs
| where TimeGenerated > ago(30d) // Change this if necessary
| where UserPrincipalName startswith"" // Add UPN Here
| extend city_  = tostring(LocationDetails.city) 
| extend state_ = tostring(LocationDetails.state) 
| extend countryOrRegion_ = tostring(LocationDetails.countryOrRegion) 
| extend latitude_  = tostring(parse_json(tostring(LocationDetails.geoCoordinates)).latitude) 
| extend longitude_ = tostring(parse_json(tostring(LocationDetails.geoCoordinates)).longitude) 
| order by TimeGenerated asc , city_ asc
| serialize 
| extend pLat = prev(latitude_,1)
| extend pLon = prev(longitude_,1)
| extend distance_in_miles = iif(isnotempty(pLat),tostring(round(geo_distance_2points(todouble(longitude_), todouble(latitude_), todouble(pLon), todouble(pLat))/1609.344 ,2)),"FirstLocation")
| where distance_in_miles !="0.0"
| summarize count() by bin(TimeGenerated, 4h),                    
                       userNameLocation = strcat(UserDisplayName, city_ , countryOrRegion_),
                       IPAddress,
                       visit_order = strcat(row_number(), ".",city_),
                       MilesTravelled=distance_in_miles                                        
| project-away count_
| order by TimeGenerated asc, visit_order asc
